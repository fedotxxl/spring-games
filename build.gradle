import groovy.io.FileType
import groovy.json.JsonOutput
import io.soyuz.build.BuildPaths
import org.apache.commons.io.FilenameUtils

apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'

project.group = "ru.grails"
project.version = "1.0"

project.ext {
    springVersion = '3.2.4.RELEASE'
    basePackage = "ru.grails"
    beanTypes = ["controllers", "services", "conf"]
    plugins = []
    soyuz = [paths: new BuildPaths(new File("${buildDir}/jopa/"))]
    jopaBuild = new File("${buildDir}/jopa/")
    jopaStatic = new File(jopaBuild, "static")
    jopaClasses = new File(jopaBuild, "classes")
    descriptorFileName = "app.json"
}

buildscript{
    repositories{
        mavenCentral()
        mavenLocal()
    }
    dependencies{
        classpath 'org.apache.commons:commons-io:1.3.2'
        classpath 'io.soyuz:soyuz-build:1.0.SNAPSHOT'
    }
}

allprojects {
    apply plugin: 'java'

    sourceSets {
        main {
            java {
                srcDir ("src")
            }

            resources {
                srcDir ("resources")
            }
        }

        assets {
            resources {
                srcDir ("static")
            }
        }
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url 'http://maven.springframework.org/snapshot/'
        }
    }

    configurations {
        // configuration that holds jars to copy into lib
        jopaPlugin
        jopaInlinePlugin
        jopaBuildConfiguration
    }


    task jopa(dependsOn: 'compileJava') << {
        println sourceSets.main.output.classesDir
        println sourceSets.main.output.resourcesDir
        println buildDir

        println configurations.compile.collect { File file -> file }
        println configurations.jopaPlugin.collect { File file -> file }
    }

    task collectLists(dependsOn: 'compileJava') << {
        println ":${project.name}:collectLists"

        def dir
        def build = sourceSets.main.output.classesDir
        def beans = [:].withDefault {[]}
        def parent = new File(build, packageToPath(basePackage))
        if (parent.exists()) {
            beanTypes.each {
                dir = new File(parent,  it)
                if (dir.exists()) {
                    dir.eachFileRecurse (FileType.FILES) { file ->
                        def path = FilenameUtils.removeExtension(file.path)
                        def relativePath = getRelativePath(build, new File(path))
                        beans[it] << pathToPackage(relativePath)
                    }
                }
            }
        }

        def path = new File("${jopaBuild}/plugins/")
        path.mkdirs();
        new File(path, descriptorFileName) << JsonOutput.toJson(beans)
    }

    task copyClasses(type: Copy) {
        from(sourceSets.main.output.classesDir)
        into("${buildDir}/jopa/src")
    }

    task copyStatic(type: Copy) {
        from(sourceSets.main.output.resourcesDir)
        into("${buildDir}/jopa/static")
    }

    task buildClasses(type: Copy) {
        into jopaClasses
        from sourceSets.main.output
    }
}

subprojects {

    project.ext {
        descriptorFileName = "plugin.${project.name}.json"
    }

    task buildStatic(type: Copy) {
        into new File(jopaStatic, project.name)
        from project.sourceSets.assets.allSource
    }
}

task jopaCompile(dependsOn: ['compileJava', 'copyClasses', 'copyStatic', 'copyLibs', 'jopa'])

task jopaCompileSubs << {
    tasks.compileJava.execute()
    tasks.collectLists.execute()
    tasks.copyClasses.execute()
    tasks.copyStatic.execute()
    tasks.copyLibs.execute()
}


task copyLibs(type: Copy) {
    from configurations.compile
    into new File(jopaBuild, "libs")
    exclude '**/*.zip'
}

task copySpringLoaded(type: Copy) {
    from configurations.jopaBuildConfiguration
    into "${jopaBuild}/build"

    rename { String fileName ->
        if (fileName.startsWith("springloaded")) {
            return "springloaded.jar"
        } else {
            return fileName
        }
    }
}

/**
 * This is support task. Don't call it manually! Instead call unpackPlugins
 * http://gradle.1045684.n5.nabble.com/dependsOn-and-configurations-td5145208.html
 */
task configurePluginsToUnpack {
    dependsOn configurations.compile
    doLast {
        configurations.compile.each { file ->
            if (file.name.endsWith(".zip")) {
                unpackPlugins.from(zipTree(file))
            }
        }
    }
}

task unpackPlugins(type: Copy, dependsOn: configurePluginsToUnpack) {
    into jopaBuild
}

dependencies {
    compile project(':subProject')

    //framework dependencies
    compile "io.soyuz:soyuz-app:1.0.SNAPSHOT"
    compile "io.soyuz:soyuz-build:1.0.SNAPSHOT"
    jopaBuildConfiguration "com.springsource.springloaded:springloaded-core:1.1.1.BUILD-SNAPSHOT"

    //plugin
    compile "ru.grails.plugins:spring-games-plugin:1.0"
}


task jopaConfig() << {
//    println configurations.jopaPlugin.dependencies.files.collect { it }
}

def getPlugins(def pResolvedDependency) {
    pResolvedDependency.children.each{
        getPlugins(it)
    }

    println pResolvedDependency.moduleGroup

    if(pResolvedDependency.moduleGroup in ["ru.grails.plugin", "joda-time"]) {
        plugins << pResolvedDependency
        println 'adding'
    }
}

task printPlugins() << {
//    println "  Project:" + project.name
//    project.configurations.each { conf ->
//        println "    Configuration: ${conf.name}"
//        conf.allDependencies.each { dep ->
//            println "      ${dep.group}:${dep.name}:${dep.version}"
//        }
//    }


//    configurations.runtime.resolvedConfiguration.firstLevelModuleDependencies.each{directDep ->
//        getPlugins(directDep)
//    }

    println configurations.runtime.resolvedConfiguration.resolvedArtifacts.findAll {
        println "${it.name} ${it.moduleVersion.id}"
    }

    println plugins
}

task zipPlugin(type:Zip, dependsOn: 'collectLists') {
    into("/static/plugins/${project.name}") {
        from sourceSets.assets.allSource
    }

    into("/plugins") {
        from new File("${buildDir}/jopa/plugins/app.json")
        rename { String fileName ->
            "descriptor.${project.name}.json"
        }
    }
}

task buildStatic(type: Copy) {
    into jopaStatic
    from project.sourceSets.assets.allSource
}

publishing {
    publications {
        def srcArtifactId = "${project.name}-src"

        impl(MavenPublication) {
            groupId 'ru.grails.plugins'
            artifactId srcArtifactId

            from components.java
        }
        api(MavenPublication) {
            groupId 'ru.grails.plugins'

            artifact zipPlugin

            //adding jar as dependency... not the best solution
            pom.withXml {
                def dependency = asNode().appendNode('dependencies').appendNode('dependency')
                dependency.appendNode('groupId', 'ru.grails.plugins')
                dependency.appendNode('artifactId', srcArtifactId)
                dependency.appendNode('version', project.version)
                dependency.appendNode('scope', 'runtime')
            }
        }
    }
    repositories {
        maven {
            url "$buildDir/repo" // change to point to your repo, e.g. http://my.org/repo
        }
    }
}


public static String packageToPath(String p) {
    return p.replace(".", "/");
}

public static String pathToPackage(String p) {
    return p.replace("/", ".");
}

public static String getRelativePath(File root, File full) {
    return root.toURI().relativize( full.toURI() ).toString()
}